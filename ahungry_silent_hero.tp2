BACKUP ~ahungry_silent_hero/backup~
AUTHOR ~Ahungry~
VERSION 1.0

AUTO_TRA ~ahungry_silent_hero/lang/%s~
// per CamDawg (thanks!):
// ^^ That basically tells WeiDU to default to USING ahungry_silent_hero/lang/%LANGUAGE%/foo.tra~ when you compile foo.d.
// %LANGUAGE% gets set to the second parameter (english) in the LANGUAGE block selected at install.

LANGUAGE
  ~English~
  ~english~
  ~ahungry_silent_hero/lang/english/game.tra~
  ~ahungry_silent_hero/lang/english/weidu.tra~

BEGIN @001 DESIGNATED 1 LABEL ahungry_silent_hero
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~) @002

OUTER_SET imoen_hello = STATE_WHICH_SAYS 1203 IN ~ahungry_silent_hero/lang/%s/game.tra~ FROM ~imoen2j~

// PRINT ~Ref was: %imoen_hello%~

ACTION_IF ("%imoen_hello%" > "-1") BEGIN
  PRINT ~Adding silent dialogues for Core Game.~
  COMPILE EVALUATE_BUFFER ~ahungry_silent_hero/dialogues/core~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~cmboun7.dlg~) BEGIN
  PRINT ~Adding silent dialogues for Dark Horizons.~
  COMPILE EVALUATE_BUFFER ~ahungry_silent_hero/dialogues/darkhorizons~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~tic0scrl.itm~) BEGIN
  PRINT ~Adding silent dialogues for BG1 Romantic Encounters.~

  OUTER_SET phlydia_bg1re_1 = STATE_WHICH_SAYS 10003 IN ~ahungry_silent_hero/lang/%s/game.tra~ FROM ~phlydi~
  OUTER_SET phlydia_bg1re_2 = STATE_WHICH_SAYS 10004 IN ~ahungry_silent_hero/lang/%s/game.tra~ FROM ~phlydi~

  ACTION_IF ("%phlydia_bg1re_1%" > "-1" AND "%phlydia_bg1re_2%" > "-1") BEGIN
    PRINT ~Patching phlydia dialog.~
    COMPILE EVALUATE_BUFFER ~ahungry_silent_hero/dialogues/bg1re/phlydia.d~
  END
  ACTION_IF ("%phlydia_bg1re_1%" < "0" OR "%phlydia_bg1re_2%" < "0") BEGIN
    PRINT ~Skipping phlydia dialog, dialog strings are missing or duplicated.~
  END
END
